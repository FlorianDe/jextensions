plugins {
    id 'java-library'
    id 'maven-publish'
}

def targetJvmVersion = JavaLanguageVersion.of(8);
def targetTestJvmVersion = JavaLanguageVersion.of(21);

ext {
    slf4jVersion = '2.0.16'
    junitVersion = '5.11.3'
}

repositories {
    mavenCentral()
}

dependencies {
    api "org.slf4j:slf4j-api:$slf4jVersion"
    testImplementation "org.slf4j:slf4j-simple:$slf4jVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

java {
    toolchain {
        languageVersion = targetJvmVersion
    }
}
compileTestJava {
    javaCompiler.set(javaToolchains.compilerFor {
        languageVersion.set(targetTestJvmVersion)
    })
}
test {
    javaLauncher.set(javaToolchains.launcherFor {
        languageVersion.set(targetTestJvmVersion)
    })
    testLogging {
        events "FAILED"
    }
    useJUnitPlatform()
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/FlorianDe/jextensions")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
